#!/usr/bin/env raku
use lib '../lib';
use CLI::AWS::EC2-Simple;

# first go `aws configure` to populate $HOME/.aws/credentials

#`[
## for cmds list & nuke
my $s = Session.new;
say $s.instance-ids;
say $s.instance-kps;

my $i = Instance.new;

$i.eip-associate;
$i.public-ip-address.say;

$i.connection-str.say;
$i.connect;

#$i.terminate;
say $i.state;
#]


enum Command <list launch connect state terminate nuke>;

sub MAIN(
    Command $cmd,                           #= One of <list launch connect state terminate nuke>
    Bool   :$eip,                           #= Allocates (if needed) and Associates Elastic IP
    Bool   :$y,                             #= Silence confirmation
    Str    :$id, # where /'i-' \w ** 16/,   #= Running InstanceId of form 'i-0785d8bd98b5f458b'
) {
    say $cmd;

    my $s = Session.new;

    sub focus-id {
        if $id {
            if $id âˆˆ $s.instance-ids.Set {
                return $id
            } else {
                die "Instance Id $id not found in this Session"
            }
        } else {
            if $s.instance-ids.elems == 1 {
                return $s.instance-ids[0] 
            } else {
                return slurp './.racl-temp'
            }
        }
    }

    given $cmd {
        when 'list' {
            $s.instance-ids.say
        }
        when 'launch' {
            if $id  {die 'cannot launch an existing Instance'}

            my $i = Instance.new(:$s);
            $i.launch;
            
            spurt './.racl-temp', $i.id;     # remember last one

            if $eip { $i.eip-associate }

            $i.id.say
        }
        when 'state' {
            Instance.new( :$s, id => focus-id ).state.say
        }
        when 'connect' {
            Instance.new( :$s, id => focus-id ).connect
        }
        when 'terminate' {
            Instance.new( :$s, id => focus-id ).terminate
        }
        when 'nuke' {
            unless $y {                     # silence check
                my $check = prompt "This will terminate all instances. You sure (y/N)? ";

                unless $check eq 'y' { die "Aborting operation..." }
            }

            for $s.instance-ids -> $id {
                Instance.new( :$s, :$id ).terminate
            }
        }
    }
}

